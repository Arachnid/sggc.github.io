---
layout: submission
title: "Submission by 0x00f9474f1f603de1c3ba6fbe525de1ffb389125b for challenge Sort"
submitter: "0x00f9474f1f603de1c3ba6fbe525de1ffb389125b"
wild: False
challenge: Sort
gas: 876789
submitted: 2018-06-03 15:54:42.062152+00:00
---
```solidity
pragma solidity 0.4.24;

contract Sort {

    /**
     * @dev Sorts a list of integers in ascending order.
     *
     * The input list may be of any length.
     *
     * @param input The list of integers to sort.
     * @return The sorted list.
     */
    function sort(uint[] input) public pure returns(uint[]) {
        if (input.length != 0) {
            quicksort(input, 0, input.length - 1);
        }
        return input;
    }

    function quicksort(uint[] input, uint lo, uint hi) public pure {
        if (lo < hi) {
            if (hi - lo > 8) {
                uint p = partition(input, lo, hi);
                quicksort(input, lo, p);
                quicksort(input, p + 1, hi);
            }
            else {
                //insertion sort
                for (uint i = lo + 1; i <= hi; ++i) {
                    uint val = input[i];
                    uint j = i - 1;
                    while (j != uint(-1)) {
                        uint a = input[j];
                        if (a <= val) {
                            break;
                        }
                        input[j+1] = a;
                        --j;
                    }
                    input[j+1] = val;
                }
             }
        }
    }

    function partition(uint[] input, uint lo, uint hi) public pure returns(uint) {
        uint pivot = input[(hi + lo) / 2];
        uint i = lo - 1;
        uint j = hi + 1;
        while(true) {
            do {
                ++i;
            }
            while (input[i] < pivot);

            do {
                --j;
            }
            while (input[j] > pivot);

            if (i >= j) {
                return j;
            }
            uint temp = input[i];
            input[i] = input[j];
            input[j] = temp;
        }
    }
}


```
