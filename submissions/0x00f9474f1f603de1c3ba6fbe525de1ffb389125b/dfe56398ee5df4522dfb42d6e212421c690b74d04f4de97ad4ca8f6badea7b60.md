---
layout: submission
title: "Submission by 0x00f9474f1f603de1c3ba6fbe525de1ffb389125b for challenge Sort"
submitter: "0x00f9474f1f603de1c3ba6fbe525de1ffb389125b"
wild: False
challenge: Sort
gas: 903909
submitted: 2018-05-30 20:30:43.961375+00:00
---
```solidity
pragma solidity 0.4.24;

contract Sort {

    /**
     * @dev Sorts a list of integers in ascending order.
     *
     * The input list may be of any length.
     *
     * @param input The list of integers to sort.
     * @return The sorted list.
     */
    function sort(uint[] input) public pure returns(uint[]) {
        if (input.length != 0) {
            quicksort(input, 0, input.length - 1);
        }
        return input;
    }

    function quicksort(uint[] input, uint lo, uint hi) public pure {
        if (lo < hi) {
            if (hi - lo > 8) {
                uint p = partition(input, lo, hi);
                quicksort(input, lo, p);
                quicksort(input, p + 1, hi);
            }
            else {
                insertionSort(input, lo, hi);
            }
        }
    }

    function partition(uint[] input, uint lo, uint hi) public pure returns(uint) {
        uint pivot = input[(hi + lo) / 2];
        uint i = lo - 1;
        uint j = hi + 1;
        while(true) {
            do {
                ++i;
            }
            while (input[i] < pivot);

            do {
               --j;
            }
            while (input[j] > pivot);

            if (i < j) {
                uint temp = input[i];
                input[i] = input[j];
                input[j] = temp;
            }
            else {
                return j;
           }
        }
    }

    function insertionSort(uint[] input, uint lo, uint hi) public pure {
        for (uint i = lo + 1; i <= hi; ++i) {
            uint val = input[i];
            uint j = i - 1;
            while (j != uint(-1) && input[j] > val) {
                input[j+1] = input[j];
                --j;
            }
            input[j+1] = val;
        }
    }
}


```
