---
layout: submission
title: "Submission by 0xec3281124d4c2fca8a88e3076c1e7749cfecb7f2 for challenge BrainFuck"
submitter: "0xec3281124d4c2fca8a88e3076c1e7749cfecb7f2"
wild: True
challenge: BrainFuck
gas: 2015189
submitted: 2018-06-09 05:02:12.517374+00:00
---
```solidity
/**
 * This file is part of the 1st Solidity Gas Golfing Contest.
 *
 * This work is licensed under Creative Commons Attribution ShareAlike 3.0.
 * https://creativecommons.org/licenses/by-sa/3.0/
 */

pragma solidity 0.4.24;


contract BrainFuck {
    /**
     * @dev Executes a BrainFuck program, as described at https://en.wikipedia.org/wiki/Brainfuck.
     *
     * Memory cells, input, and output values are all expected to be 8 bit
     * integers. Incrementing past 255 should overflow to 0, and decrementing
     * below 0 should overflow to 255.
     *
     * Programs and input streams may be of any length. The memory tape starts
     * at cell 0, and will never be moved below 0 or above 1023. No program will
     * output more than 1024 values.
     *
     * @param program The BrainFuck program.
     * @param input The program's input stream.
     * @return The program's output stream. Should be exactly the length of the
     *          number of outputs produced by the program.
     */
    function execute(bytes memory program, bytes memory input) public pure returns(bytes) {
        bytes memory outputMem = new bytes(2048);
        assembly {
            let ipp := 0
            let opp := 0
            let dp := 0
            let programStart := add(program, 0x20)
            let memStart := add(outputMem, 0x420)
            for {let ip := programStart } lt(ip, add(programStart, mload(program))) {ip := add(ip, 1)} {
                switch byte(0, mload(ip))
                case 0x2B {
                    let dest := add(memStart, dp)
                    mstore8(dest, add(byte(0, mload(dest)), 1))
                }
                case 0x2D {
                    let dest := add(memStart, dp)
                    mstore8(dest, sub(byte(0, mload(dest)), 1))
                }
                case 0x3E {
                    dp := add(dp, 1)
                }
                case 0x3C {
                    dp := sub(dp, 1)
                }
                case 0x2E {
                    mstore8(add(add(outputMem, 0x20), opp), byte(0, mload(add(memStart, dp))))
                    opp := add(opp, 1)
                }
                case 0x2C {
                    mstore8(add(memStart, dp), byte(0, mload(add(add(input, 0x20), ipp))))
                    ipp := add(ipp, 1)
                }
                case 0x5B {
                    switch byte(0, mload(add(memStart, dp)))
                    case 0 {
                        let depth := 1
                        for {let i := add(ip, 1)} lt(i, add(programStart, mload(program))) {i := add(i, 1)} {
                            switch byte(0, mload(i))
                            case 0x5D {
                                depth := sub(depth, 1)
                                switch depth
                                case 0  {
                                    ip := i
                                    i := add(programStart, mload(program))
                                }
                            }
                            case 0x5B {
                                depth := add(depth, 1)
                            }
                        }
                    }
                }
                case 0x5D {
                    switch iszero(byte(0, mload(add(memStart, dp))))
                    case 0 {
                        let depth := 1
                        for {let i := sub(ip, 1)} gt(i, program) { i := sub(i, 1)} {
                            switch byte(0, mload(i))
                            case 0x5B {
                                depth := sub(depth, 1)
                                switch depth
                                case 0 {
                                    ip := i
                                    i := program
                                }
                            }
                            case 0x5D {
                                depth := add(depth, 1)
                            }
                        }
                    }
                }
            }
            mstore(outputMem, opp)
        }

        return outputMem;
    }
}

```
