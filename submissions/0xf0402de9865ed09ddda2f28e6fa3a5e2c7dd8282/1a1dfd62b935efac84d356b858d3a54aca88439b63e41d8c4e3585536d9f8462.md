---
layout: submission
title: "Submission by 0xf0402de9865ed09ddda2f28e6fa3a5e2c7dd8282 for challenge Sort"
submitter: "0xf0402de9865ed09ddda2f28e6fa3a5e2c7dd8282"
wild: False
challenge: Sort
gas: 1717119
submitted: 2018-05-24 23:27:16.264008+00:00
---
```solidity
pragma solidity 0.4.24;

contract Sort {

    /**
     * @dev Sorts a list of integers in ascending order.
     *
     * The input list may be of any length.
     *
     * @param input The list of integers to sort.
     * @return The sorted list.
     */
    function sort(uint[] input) public pure returns (uint[]) {
        if (input.length == 0) {
            return input;
        }
        
        uint min;
        uint numBuckets;
        uint increment;
        (min, numBuckets, increment) = getBucketInfo(input);
        
        uint[][] memory buckets = new uint[][](numBuckets);
        uint[] memory bucketLens = new uint[](numBuckets);
        uint[] memory bucketHeights = new uint[](numBuckets);
        
        for (uint x = 0; x < input.length; x++) {
            uint incrBucket = (input[x] - min) / increment;
            bucketLens[incrBucket] = bucketLens[incrBucket] + 1;
        }
        
        for (x = 0; x < numBuckets; x++) {
            buckets[x] = createEmptyBucket(bucketLens[x]);
        }
        
        for (x = 0; x < input.length; x++) {
            uint bucketNum = (input[x] - min) / increment;
            uint[] memory bucket = buckets[bucketNum];
            bucket[bucketHeights[bucketNum]] = input[x];
            bucketHeights[bucketNum] = bucketHeights[bucketNum] + 1;
        }
        
        min = 0;
        for (x = 0; x < numBuckets; x++) {
            uint[] memory sortedBucket = insertionSort(buckets[x], bucketLens[x]);
            
            for (increment = 0; increment < bucketLens[x]; increment++) {
                input[min++] = sortedBucket[increment];
            }
        }
        
        return input;
    }
    
    function createEmptyBucket(uint len) public pure returns (uint[] memory bucket) {
        uint[] memory newBucket = new uint[](len);
        return newBucket;
    }
    
    function getBucketInfo(uint[] input) public pure returns (uint minimum, uint numBuckets, uint increment) {
        minimum = min(input);
        numBuckets = ceil(input.length, 16);
        increment = ceil(max(input) - minimum + 1, numBuckets);
    }
    
    function insertionSort(uint[] bucket, uint bucketLen) public pure returns (uint[]) {
        for (uint i = 0; i < bucketLen; i++) {
            uint min = bucket[i];
            uint minIndex = i;
            for (uint j = i + 1; j < bucketLen; j++) {
                uint jval = bucket[j];
                if (jval < min) {
                    min = jval;
                    minIndex = j;
                }
            }
            uint temp = bucket[i];
            bucket[i] = min;
            bucket[minIndex] = temp;
        }
        
        return bucket;
    }
    
    function min(uint[] input) public pure returns (uint) {
        uint minValue = input[0];
        
        for (uint x = 1; x < input.length; x++) {
            uint test = input[x];
            if (test < minValue) {
                minValue = test;
            }
        }
        
        return minValue;
    }
    
    function max(uint[] input) public pure returns (uint) {
        uint maxValue = input[0];
        
        for (uint x = 1; x < input.length; x++) {
            uint test = input[x];
            if (test > maxValue) {
                maxValue = test;
            }
        }
        
        return maxValue;
    }

    function ceil(uint numerator, uint denominator) public pure returns (uint) {
        return (numerator + denominator - 1) / denominator;
    }

}
```
