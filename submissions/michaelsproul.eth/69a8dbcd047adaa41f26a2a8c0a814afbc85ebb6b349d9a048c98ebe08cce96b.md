---
layout: submission
title: "Submission by michaelsproul.eth for challenge IndexOf"
submitter: "michaelsproul.eth"
wild: False
challenge: IndexOf
gas: 334175
submitted: 2018-06-02 06:50:32.015617+00:00
---
```solidity
/**
 * This file is part of the 1st Solidity Gas Golfing Contest.
 *
 * This work is licensed under Creative Commons Attribution ShareAlike 3.0.
 * https://creativecommons.org/licenses/by-sa/3.0/
 *
 * Implementation by Michael Sproul (https://sproul.xyz)
 */

pragma solidity 0.4.24;

contract IndexOf {
    /**
     * @dev Returns the index of the first occurrence of `needle` in `haystack`,
     *      or -1 if `needle` is not found in `haystack`.
     *
     * Input strings may be of any length <2^255.
     *
     * @param haystack_s The string to search.
     * @param needle_s The string to search for.
     * @return The index of `needle` in `haystack`, or -1 if not found.
     */
    function indexOf(string haystack_s, string needle_s) public pure returns (int) {
        bytes memory haystack = bytes(haystack_s);
        bytes memory needle = bytes(needle_s);
        uint n = haystack.length;
        uint m = needle.length;
        uint i = m;
        uint stop = n - m;
        uint j;
        bool matched;
        uint x = 1;

        if (n < m) {
            return -1;
        }

        // Rabin-Karp algorithm
        // Define our hash function as follows:
        // hash(x) = sum(256^i * x_i) mod 101
        // Treat the string as most-significant-byte first

        uint needle_hash;
        uint substring_hash;
        for (; i > 0; i = j) {
            j = i - 1;
            needle_hash = addmod(needle_hash, x * uint(needle[j]), 101);
            substring_hash = addmod(substring_hash, x * uint(haystack[j]), 101);
            x = mulmod(256, x, 101);
        }

        // x now equals 256^m

        for (; i < stop; i = i + 1) {
            if (needle_hash == substring_hash) {
                // Check that all the characters actually match
                matched = true;
                for (j = 0; j < m; j = j + 1) {
                    if (haystack[i + j] != needle[j]) {
                        matched = false;
                        break;
                    }
                }

                if (matched) {
                    return int(i);
                }
            }

            // Else, update the hash for the next iteration
            uint y = 256 * substring_hash;

            // 2. Subtract the most significant char (position i)
            uint z = y + x * (303 - uint(haystack[i]));

            // 3. Add the next char
            substring_hash = addmod(z, uint(haystack[i + m]), 101);
        }

        // Check for a match once more on the way out
        if (needle_hash == substring_hash) {
            // Check that all the characters actually match
            matched = true;
            for (j = 0; j < m; j = j + 1) {
                if (haystack[i + j] != needle[j]) {
                    matched = false;
                    break;
                }
            }

            if (matched) {
                return int(i);
            }
        }

        return -1;
    }
}
```
