---
layout: submission
title: "Submission by michaelsproul.eth for challenge HexDecoder"
submitter: "michaelsproul.eth"
wild: False
challenge: HexDecoder
gas: 1168318
submitted: 2018-06-05 11:00:10.407059+00:00
---
```solidity
/**
 * This file is part of the 1st Solidity Gas Golfing Contest.
 *
 * This work is licensed under Creative Commons Attribution ShareAlike 3.0.
 * https://creativecommons.org/licenses/by-sa/3.0/
 *
 * Implementation by Michael Sproul (https://sproul.xyz)
 */

pragma solidity 0.4.24;

contract HexDecoder {
    /**
     * @dev Decodes a hex-encoded input string, returning it in binary.
     *
     * Input strings may be of any length, but will always be a multiple of two
     * bytes long, and will not contain any non-hexadecimal characters.
     *
     * @param input The hex-encoded input.
     * @return The decoded output.
     */
    function decode(string input) public pure returns (bytes output) {
        bytes memory inputBytes = bytes(input);
        uint m = inputBytes.length;
        m = m / 2;
        output = new bytes(m);
        uint x;
        uint y;
        for (uint i; i < m; i++) {
            // This little formula works because only '0'-'9' have bit 0x10 set,
            // so when we mask and add 9 we get 25 + i = i (mod 25).
            // For 'a'-'f' and 'A'-'F', the mask and add takes 0x41 -> 0x01 -> 10 and so on.
            x = uint(inputBytes[i + i]);
            x = 16 * addmod(x & 0x1F, 9, 25);
            y = uint(inputBytes[i + i + 1]);
            output[i] = byte(x | addmod(y & 0x1F, 9, 25));
        }
    }
}
```
