---
layout: submission
title: "Submission by 0xea674fdde714fd979de3edf0f56aa9716b898ec8 for challenge Unique"
submitter: "0xea674fdde714fd979de3edf0f56aa9716b898ec8"
wild: False
challenge: Unique
gas: 529902
submitted: 2018-06-27 02:48:38.041734+00:00
---
```solidity
/**
 * This file is part of the 1st Solidity Gas Golfing Contest.
 *
 * This work is licensed under Creative Commons Attribution ShareAlike 3.0.
 * https://creativecommons.org/licenses/by-sa/3.0/
 */

pragma solidity 0.4.24;

contract Unique {
    /**
     * @dev Removes all but the first occurrence of each element from a list of
     *      integers, preserving the order of original elements, and returns the list.
     *
     * The input list may be of any length.
     *
     * @param input The list of integers to uniquify.
     * @return The input list, with any duplicate elements removed.
     */
    function uniquify(uint[] input) public pure returns(uint[] ret) {
        uint len = input.length;
        if (len <= 1) {
            return input;
        }

        uint ptr = 0;
        ret = new uint[](len * 2);

        for(uint i = 0; i < len; i++) {
            uint value = input[i];
            uint valueMod = value % (len * 2);
            uint storedValue = ret[valueMod];
            if (storedValue == 0) {
                ret[valueMod] = value + 1;
                input[ptr++] = value;
            } else if (storedValue != value + 1) {

                for (
                    bool found = false;
                    !found && ret[valueMod] != 0;
                    valueMod = (valueMod + 1) % (len * 2)
                ) {
                    found = (ret[valueMod] == value + 1);
                }
                if (!found) {
                    ret[valueMod] = value + 1;
                    input[ptr++] = value;
                }
            }
        }

        ret = new uint[](ptr);

        for(i = 0; i < ptr; i++) {
            ret[i] = input[i];
        }

        return ret;
    }

}

```
