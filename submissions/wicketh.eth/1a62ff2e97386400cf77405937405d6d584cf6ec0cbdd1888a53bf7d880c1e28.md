---
layout: submission
title: "Submission by wicketh.eth for challenge IndexOf"
submitter: "wicketh.eth"
wild: False
challenge: IndexOf
gas: 277868
submitted: 2018-06-09 16:59:14.391928+00:00
---
```solidity
pragma solidity ^0.4.23;

contract IndexOf {
    
    /// @author Remco Bloemen <remco@wicked.ventures>

    function indexOf(string haystack, string needle)
        public pure
        returns(int256)
    {
        uint256 hl = bytes(haystack).length;
        uint256 nl = bytes(needle).length;
        if(nl > hl) {
            return -1;
        }
        if(nl == 0) {
            return 0;
        }
        
        // Rabin-Karp without verify
        uint256 needleHash = 0;
        for(uint i = 0; i < nl; i++) {
            needleHash += needleHash;
            needleHash += read1(needle, i);
        }
        uint256 factor = 2 ** (nl - 1);
        
        uint256 hayHash = 0;
        for(i = 0; i < nl; i++) {
            hayHash += hayHash;
            hayHash += read1(haystack, i);
        }
        if (hayHash == needleHash) {
            return 0;
        }
        for (; i < hl; i++) {
            hayHash -= read1(haystack, i - nl) * factor;
            hayHash += hayHash;
            hayHash += read1(haystack, i);
            if (hayHash == needleHash) {
                return int256(i - nl + 1);
            }
        }
        return -1;
    }
    
    function read1(string memory a, uint256 i)
        private pure
        returns (uint8)
    {
        return uint8(bytes(a)[i]);
    }
}

```
