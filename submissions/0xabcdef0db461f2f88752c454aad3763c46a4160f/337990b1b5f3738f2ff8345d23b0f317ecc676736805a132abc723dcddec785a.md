---
layout: submission
title: "Submission by 0xabcdef0db461f2f88752c454aad3763c46a4160f for challenge HexDecoder"
submitter: "0xabcdef0db461f2f88752c454aad3763c46a4160f"
wild: True
challenge: HexDecoder
gas: 459914
submitted: 2018-05-31 05:36:40.107314+00:00
---
```solidity
/**
 * This file is part of the 1st Solidity Gas Golfing Contest.
 *
 * This work is licensed under Creative Commons Attribution ShareAlike 3.0.
 * https://creativecommons.org/licenses/by-sa/3.0/
 */

pragma solidity 0.4.24;

contract HexDecoder {
    /**
     * @dev Decodes a hex-encoded input string, returning it in binary.
     *
     * Input strings may be of any length, but will always be a multiple of two
     * bytes long, and will not contain any non-hexadecimal characters.
     *
     * @param input The hex-encoded input.
     * @return The decoded output.
     */
    function decode(string input) public pure returns(bytes output) {
        bytes memory input_bytes = bytes(input);
        uint output_length = input_bytes.length / 2;
        bytes memory output_bytes = new bytes(output_length);
        decode_more(input_bytes, output_bytes);
        return output_bytes;
    }

    function decode_more(bytes memory input_bytes, bytes memory output_bytes) internal pure {
        uint[55] memory dict = [
            uint(0),
            0x1, 0x2, 0x3, 0x4, 0x5, 0x6, 0x7, 0x8, 0x9,   0,   0,   0,   0,   0,   0,   0,
            0xa, 0xb, 0xc, 0xd, 0xe, 0xf,   0,   0,   0,   0,   0,   0,   0,   0,   0,   0,
              0,   0,   0,   0,   0,   0,   0,   0,   0,   0,   0,   0,   0,   0,   0,   0,
            0xa, 0xb, 0xc, 0xd, 0xe, 0xf];
        byte tmpByte;
        uint tmpUint;
        assembly {
            let inptr := add(input_bytes, 0x20)
            let outptr := add(output_bytes, 0x20)
            let outlen := mload(output_bytes)
            let outptrEnd := add(outptr, outlen)
            for { } lt(outptr, outptrEnd) { } {

                tmpByte:= mload(inptr)
                mstore(0x1f, tmpByte)

                tmpUint := mul(mload(add(dict, mul(0x20, sub(mload(0x0), 48)))), 16)
                inptr := add(inptr, 0x1)

                tmpByte:= mload(inptr)
                mstore(0x1f, tmpByte)

                tmpUint := add(
                    tmpUint,
                    mload(add(dict, mul(0x20, sub(mload(0x0), 48)))))
                inptr := add(inptr, 0x1)

                mstore8(outptr, tmpUint)

                outptr := add(outptr, 0x1)
            }
        }
    }
}
```
