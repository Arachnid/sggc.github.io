---
layout: submission
title: "Submission by ricmoo.firefly.eth for challenge Unique"
submitter: "ricmoo.firefly.eth"
wild: True
challenge: Unique
gas: 2826071
---
```solidity
/**
 * This file is part of the 1st Solidity Gas Golfing Contest.
 *
 * This work is licensed under Creative Commons Attribution ShareAlike 3.0.
 * https://creativecommons.org/licenses/by-sa/3.0/
 *
 *  Author: Richard Moore <me@ricmoo.com>
 *  Date:   May 24, 2018
 */

pragma solidity 0.4.24;

contract Unique {

    function isUnique(uint[] input, uint length, uint value) private pure returns(bool) {
        for (uint i = 0; i < length; i++) {
            if (input[i] == value) { return false; }
        }
        return true;
    }

    /**
     * @dev Removes all but the first occurrence of each element from a list of
     *      integers, preserving the order of original elements, and returns the list.
     *
     * The input list may be of any length.
     *
     * @param input The list of integers to uniquify.
     * @return The input list, with any duplicate elements removed.
     */
    function uniquify(uint[] input) public pure returns(uint[] ret) {

        uint256 bloomFilter = 0;
        uint ptr = 0;
        for (uint256 i = 0; i < input.length; i++) {
            uint256 v = input[i];
            uint256 filter = (uint256(1) << uint8(0 - v));
            if ((bloomFilter & filter) != filter || isUnique(input, i, v)) {
                input[ptr++] = v;
                bloomFilter |= filter;
            }
        }

        assembly {
            mstore(input, ptr)
        }
        return input;
    }
}
```
