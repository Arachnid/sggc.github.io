---
layout: submission
title: "Submission by 0xff3c2241cd767b735b26f3bd62c39b57ce02123d for challenge Unique"
submitter: "0xff3c2241cd767b735b26f3bd62c39b57ce02123d"
wild: False
challenge: Unique
gas: 1248523
submitted: 2018-05-24 20:25:58.221634+00:00
---
```solidity
/**
 * This file is part of the 1st Solidity Gas Golfing Contest.
 *
 * This work is licensed under Creative Commons Attribution ShareAlike 3.0.
 * https://creativecommons.org/licenses/by-sa/3.0/
 */

pragma solidity 0.4.24;

contract Unique {

    // This implementation:     1024798

    /**
     * @dev Removes all but the first occurrence of each element from a list of
     *      integers, preserving the order of original elements, and returns the list.
     *
     * The input list may be of any length.
     *
     * @param input The list of integers to uniquify.
     * @return The input list, with any duplicate elements removed.
     */
    function uniquify(uint[] input) public pure returns(uint[] ret) {
      // Oh, we can't have mappings in memory? Then we're going to make our own rubbish one! :D
      uint256[] memory processedvalue = new uint256[](input.length+1);
      bool[] memory processedbool = new bool[](input.length+1);

      uint slot;
      uint nuniques;
      for (uint i =0; i<input.length; i+=1){
        slot = input[i] % input.length;
        while (processedbool[slot] == true && processedvalue[slot] != input[i]){
          slot +=1;
          if (slot > input.length){
            slot=0;
          }
        }
        if (processedbool[slot]==false){
          // Then we've not seen it before
          processedbool[slot] = true;
          processedvalue[slot] = input[i];
          input[nuniques] = input[i];
          nuniques += 1;
        }
      }
      // create output array
      ret = new uint[](nuniques);
      for (i = 0; i<ret.length; i+=1){
        ret[i] = input[i];
      }
      return ret;
    }

}

```
