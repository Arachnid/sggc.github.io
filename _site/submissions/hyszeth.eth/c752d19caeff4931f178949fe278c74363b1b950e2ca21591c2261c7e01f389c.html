<!doctype html>
<html lang="en">
<head>
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1, shrink-to-fit=no">
    <link rel="stylesheet" href="https://maxcdn.bootstrapcdn.com/bootstrap/4.0.0/css/bootstrap.min.css" integrity="sha384-Gn5384xqQ1aoWXA+058RXPxPg6fy4IWvTNh0E263XmFcJlSAwiGgFAW/dAiS6JXm" crossorigin="anonymous">
    <style type="text/css">
        .container {
            margin-bottom: 2em;
        }
        body {
            padding-top: 5em;
        }
    </style>
    <title></title>
</head>
<body>
    <nav class="navbar navbar-expand-lg navbar-light bg-light fixed-top">
        <a class="navbar-brand" href="#">SGGC</a>
        <button class="navbar-toggler" type="button" data-toggle="collapse" data-target="#navbarSupportedContent" aria-controls="navbarSupportedContent" aria-expanded="false" aria-label="Toggle navigation">
            <span class="navbar-toggler-icon"></span>
        </button>

        <div class="collapse navbar-collapse" id="navbarSupportedContent">
            <ul class="navbar-nav mr-auto">
                <li class="nav-item active">
                    <a class="nav-link" href="/#">Home</a>
                </li>
                <li class="nav-item">
                    <a class="nav-link" href="/#leaderboard">Leaderboard</a>
                </li>
                <li class="nav-item">
                    <a class="nav-link" href="/#faq">FAQ</a>
                </li>
            </ul>
        </div>
    </nav>
    <div class="container">
    <h1>Submission c752d19c...</h1>
    <table class="table">
        <tr><th>Challenge</th><td><a href="/challenges/indexof">String search</a></td></tr>
        <tr><th>Submitter</th><td><a href="/submissions/hyszeth.eth/">hyszeth.eth</a>
        <tr><th>Submitted at</th><td>2018-04-30</td></tr>
        <tr><th>Gas used</th><td>82208</td></tr>
    </table>
    <pre><code class="language-solidity">/**
 * This file is part of the 1st Solidity Gas Golfing Conshift.
 *
 * This work is licensed under Creative Commons Attribution ShareAlike 3.0.
 * https://creativecommons.org/licenses/by-sa/3.0/
 *
 * Author: Greg Hysen (hyszeth.eth)
 * Date: June 2018
 * Description: Search for a needle in a haystack using the Boyer-Moore algorithm,
 *              along with the bad character heuristic. For single character needles,
 *              default to a simple naive search.
 */

pragma solidity 0.4.24;

contract IndexOf {

    // Return value when `needle` is not found in `haystack`.
    int256 constant NOT_FOUND = -1;

    /**
     * @dev Returns the index of the first occurrence of `needle` in `haystack`,
     *      or -1 if `needle` is not found in `haystack`.
     *
     * Input strings may be of any length &lt;2^255.
     *
     * @param haystack The string to search.
     * @param needle The string to search for.
     * @return The index of `needle` in `haystack`, or -1 if not found.
     */
    function indexOf(string haystack, string needle)
        public
        pure
        returns(int)
    {
        // Lengths of input strings.
        uint256 haystackLength = bytes(haystack).length;
        uint256 needleLength = bytes(needle).length;

        // Handle base cases.
        if(needleLength &gt; haystackLength) return -1;
        if(needleLength == 0) return 0;
        if(needleLength == 1) {
            // Needle is a single character; default to a naive search.
            uint needleChar = uint(bytes(needle)[0]);
            uint max = haystackLength/5 * 5;
            uint idx;
            // Search in groups to save gas.
            while(idx != max) {
                if(needleChar == uint(bytes(haystack)[idx++])) return int256(--idx);
                if(needleChar == uint(bytes(haystack)[idx++])) return int256(--idx);
                if(needleChar == uint(bytes(haystack)[idx++])) return int256(--idx);
                if(needleChar == uint(bytes(haystack)[idx++])) return int256(--idx);
                if(needleChar == uint(bytes(haystack)[idx++])) return int256(--idx);
            }
            // Search remaining characters in haystack.
            while(idx != haystackLength) if(needleChar == uint(bytes(haystack)[idx++])) return int256(--idx);
            return NOT_FOUND;
        }

        // No base case was hit; run Boyer-Moore.
        return boyerMooreSearch(haystack, needle, haystackLength, needleLength);
    }

    /**
     * @dev Runs boyer-moore with bad character heuristic.
     *      We start with both the needle and haystack aligned at index 0,
     *      matching characters from right to left (starting at the end of `needle`).
     *
     *      The last instance of each character in `needle` is stored in a lookup table, `badchar`.
     *      When there is a mismatch, we handle 3 cases:
     *      1. An instance of "bad character" exists in `needle` to the left of our current index.
     *         In this case, we shift `needle` into place so these characters match.
     *      2. One or more instances of "bad character" exist in `needle`, but because we
     *         only store the last instance we can't perform an optimized shift.
     *         In this case, we shift `needle` to the right by 1.
     *      3. There are no instances of "bad character" in `needle`.
     *         In this case, shift `needle` to the right past the "bad character".
     *
     * @param haystack The string to search.
     * @param needle The string to search for.
     * @param haystackLength The length of `haystack`.
     * @param needleLength The length of `needle`.
     * @return The index of `needle` in `haystack`, or -1 if not found.
     */
    function boyerMooreSearch(string haystack, string needle, uint256 haystackLength, uint256 needleLength)
        private
        pure
        returns(int)
    {
        // Create lookup table for characters in `needle`,
        // storing only the last occurence of each.
        uint256[256] memory badchar;
        uint tmp = needleLength/5 * 5;
        uint i;
        while(i != tmp) {
            // Run in groups of 5 to save gas.
            badchar[uint(bytes(needle)[uint(i++)])] = uint(i+1);
            badchar[uint(bytes(needle)[uint(i++)])] = uint(i+1);
            badchar[uint(bytes(needle)[uint(i++)])] = uint(i+1);
            badchar[uint(bytes(needle)[uint(i++)])] = uint(i+1);
            badchar[uint(bytes(needle)[uint(i++)])] = uint(i+1);
        }
        // Fill in remaining characters.
        while(i != needleLength) badchar[uint(bytes(needle)[uint(i++)])] = uint(i+1);

        // Maximum index in `needle`
        uint maxNeedleIndex = needleLength-1;
        // Maximumn offset into `haystack`
        uint maxHaystackOffset = (haystackLength - needleLength);
        // Current offset into `haystack`
        uint256 haystackOffset;
        // Current haystack character
        uint currentHaystackChar;
        // Amount to shift needle by
        uint shift;
        // Current index into `needle`
        uint256 needleIndex;
        while(haystackOffset &lt;= maxHaystackOffset) {
            // Start searching from end of needle.
            needleIndex = maxNeedleIndex;

            // Fast forward through matches.
            while(uint(bytes(needle)[uint(needleIndex)]) == (currentHaystackChar = uint(bytes(haystack)[uint(haystackOffset + needleIndex)]))) {
                if(--needleIndex != uint(-1)) continue;
                // We found the substring!
                return int256(haystackOffset);
            }

            // We have a mismatch. Lookup the mismatched character in `badchar`.
            tmp = badchar[uint(currentHaystackChar)];
            if(tmp != 0) {
                // The mismatched character exists in `needle`
                if(tmp &lt; needleIndex) {
                    // Case #1
                    haystackOffset += needleIndex - tmp;
                    continue;
                }
                // Case #2
                ++haystackOffset;
                continue;
            }
            // Case #3
            haystackOffset += needleIndex + 1;
        }

        return NOT_FOUND;
    }
}

</code></pre>

</div>

    <script src="https://code.jquery.com/jquery-3.2.1.slim.min.js" integrity="sha384-KJ3o2DKtIkvYIK3UENzmM7KCkRr/rE9/Qpg6aAZGJwFDMVNA/GpGFF93hXpG5KkN" crossorigin="anonymous"></script>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/popper.js/1.12.9/umd/popper.min.js" integrity="sha384-ApNbgh9B+Y1QKtv3Rn7W3mgPxhU9K/ScQsAP7hUibX39j7fakFPskvXusvfa0b4Q" crossorigin="anonymous"></script>
    <script src="https://maxcdn.bootstrapcdn.com/bootstrap/4.0.0/js/bootstrap.min.js" integrity="sha384-JZR6Spejh4U02d8jOt6vLEHfe/JQGiRRSQQxSfFWpi1MquVdAyjUar5+76PVCmYl" crossorigin="anonymous"></script>
</body>
</html>
